// Setting includes a value of generic type. Currently, Setting is tested against a String of length 7 as its value.

mod string_20;
mod string_7;

use string_20::String_20;
use string_20::STRING_20_LENGTH;

use string_7::String_7;
use string_7::STRING_7_LENGTH;

const SETTING_PROPERTY_LENGTH: u16 = STRING_20_LENGTH;
const SETTING_VALUE_LENGTH: u16 = STRING_7_LENGTH;

const SETTING_PROPERTY_OFFSET: u16 = 0;
const SETTING_VALUE_OFFSET: u16
    = SETTING_PROPERTY_OFFSET
    + SETTING_PROPERTY_LENGTH;
const SETTING_LENGTH: u16
    = SETTING_VALUE_OFFSET
    + SETTING_VALUE_LENGTH;

type SerializedSetting = [u8; SETTING_LENGTH];

struct Setting {
    property: String_20,
    value: String_7,
}

impl Setting {
    fn equals(this: Setting, that: Setting) -> bool {
        String_20::equals(this.property, that.property)
            && String_7::equals(this.value, that.value)
    }

    fn deserialize(serialized: SerializedSetting) -> Setting {
        let property = {
            let mut property_bytes = [0; SETTING_PROPERTY_LENGTH];
            for i in 0..SETTING_PROPERTY_LENGTH {
                property_bytes[i] = serialized[i + SETTING_PROPERTY_OFFSET];
            }
            String_20::deserialize(property_bytes)
        };

        let value = {
            let mut value_bytes = [0; SETTING_VALUE_LENGTH];
            for i in 0..SETTING_VALUE_LENGTH {
                value_bytes[i] = serialized[i + SETTING_VALUE_OFFSET];
            }
            String_7::deserialize(value_bytes)
        };

        Setting {
            property: property,
            value: value,
        }
    }
}